REM	-----------------------------------------------
REM	
REM	ORTEC Job File : Begin_Det_Single_Cal.Job
REM   
REM	Description : Calibration of Detectors 1 & 2 for single livestock geometry
REM 
REM	Application Software : A65-BW V7.01
REM
REM	Job Version 1.0
REM
REM	Date : 25 Dec. 2014
REM
REM     -------------------------------------------------------------
REM	
REM	Description :
REM
REM	Detectors 1 and 2 Calibration for single livestock
REM
REM	5 nuclides ( default )
REM
REM	DET1_ROI5.ROI & DET2_ROI5.ROI are using for ROI reports ( default )
REM
REM	Calibration time per nuclide is 300 seconds default ( 5 secs for testing )
REM
REM	Spectra store at MCA memory after counting
REM
REM	Saved spectra C:\LCMS\Spectra\DET1_Single_Cal_n.Chn and DET2_Single_Cal_n; where n is no. of nuclide
REM
REM	ROI reports ( ASCII ) will be generated after elapsed of measuring time
REM
REM	ROI reports saved as : C:\LCMS\Data\DET1_Single_Cal_n.Dat and DET2_Single_Cal_n.dat; where n is no. of nuclide.
REM
REM	--------------------------------------------------------------


REM Close all buffers if any

CLOSEBUFFERS


REM	------------------------------------------------------
REM 	Set ZOOM -1 for minimize Maestro window after debug
REM 	------------------------------------------------------

REM	----------------------------------------------------------
REM	Stop, clear MCA and preset live time for detectors 1 & 2
REM	----------------------------------------------------------


REM ----------------------------------------------------------
SET_DETECTOR 1
STOP
CLEAR
SET_PRESET_CLEAR

REM ** modify below command for different ROI file of Det1
RECALL_ROI "C:\LCMS\ROI\DET1_ROI5.ROI"

REM ** modify below command for different counting time in second per nuclide
SET_PRESET_LIVE 5


REM ----------------------------------------------------------
SET_DETECTOR 2
STOP
CLEAR
SET_PRESET_CLEAR

REM ** modify below command for different ROI file of Det2
RECALL_ROI "C:\LCMS\ROI\DET2_ROI5.ROI"

REM ** modify below command for different counting time in second per nuclide
SET_PRESET_LIVE 5


	REM ** modify loop value for total number of nuclides
	Loop 5


	REM ***************** Start of looping ******************

	ASK_CONFIRM "Place Calibration Standard < $(Loop1) > in Position, then press OK"

	SET_DETECTOR 1
	DESCRIBE_SAMPLE "Detector 1 Nuclide $(Loop1)"
	CLEAR
	START

	SET_DETECTOR 2
	DESCRIBE_SAMPLE "Detector 2 Nuclide $(Loop1)"
	CLEAR 
	START

	WAIT 2
	WAIT

	REM Save calibration spectra of current nuclide
	REM $(Loop1) has value of 1, 2, 3, 4 and 5 for "Loop 5" command

	SET_DETECTOR 1
	SAVE "C:\LCMS\SPECTRA\DET1_Single_Cal_$(Loop1).Chn"
	WAIT
	REPORT "C:\LCMS\Data\DET1_Single_Cal_$(Loop1).Dat"
	WAIT

	SET_DETECTOR 2
	SAVE "C:\LCMS\SPECTRA\DET2_Single_Cal_$(Loop1).Chn"
	WAIT
	REPORT "C:\LCMS\Data\DET2_Single_Cal_$(Loop1).Dat"
	WAIT

	End_Loop

ASK_CONFIRM "Calibration Done O.K. !"


REM quit Maestro and return to calling program 
REM QUIT

REM * * * * Job file end * * * *

